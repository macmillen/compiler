/*
 * parser.cup -- SPL parser specification
 */


package parse;

import java_cup.runtime.*;
import sym.*;
import absyn.*;

/* Preliminaries to set up and use the scanner.  */
/*
init with {: scanner.init();              :};
scan with {: return scanner.next_token(); :};
*/
parser code {:

  public void syntax_error(Symbol currToken) {
    System.out.println(
      "**** Error: syntax error" +
      " in line " + currToken.left +
      ", column " + currToken.right
    );
    System.exit(0);
  }

:}

/* Terminals (tokens returned by the scanner). */
terminal			ARRAY, ELSE, IF, OF, PROC, REF, TYPE, VAR, WHILE, LPAREN, RPAREN, LBRACK,
					RBRACK, LCURL, RCURL, EQ, NE, LT, LE, GT, GE, ASGN, COLON, COMMA, SEMIC, PLUS,
					MINUS, STAR, SLASH;
terminal Integer	INTLIT;
terminal String		IDENT;


/* Non-terminals */

non terminal		Ty type;
non terminal		Var refExpression;
non terminal        DecList program;
non terminal		DecList parameterList;
non terminal		DecList paramListTail;
non terminal		DecList vardecs;
non terminal        Dec dec;
non terminal	    Dec typedef;
non terminal		Dec vardec;
non terminal		Dec procedure;
non terminal		Dec parameter;
non terminal		StmList statements;
non terminal		Stm procedure_call;
non terminal		Stm varassign;
non terminal		Stm while_statement;
non terminal		Stm if_statement;
non terminal		Stm statement;
non terminal		ExpList argumentList;
non terminal		ExpList argumentListTail;
non terminal		Exp argument;
non terminal		Exp expression;
non terminal		Exp simple_expression;
non terminal		Exp term;
non terminal		Exp unary;
non terminal		Exp factor;

start with program;
program                 ::= {:RESULT = new DecList();:} | dec:d program:decs {:RESULT = new DecList(d, decs);:}
    ;
dec 			        ::= typedef:dec {: RESULT = dec;:} |
                            procedure:dec {: RESULT = dec;:}
    ;
typedef                 ::= TYPE IDENT:ident EQ type:ty SEMIC
                            {:RESULT = new TypeDec(identleft, identright, Sym.newSym(ident.toString()), ty);:}
    ;
type                    ::= IDENT:ident 
                            {:RESULT = new NameTy(identleft, identright, Sym.newSym(ident.toString()));:} |
                            ARRAY LBRACK INTLIT:intlit RBRACK OF type:ty {: RESULT = new ArrayTy(intlitleft, intlitright, intlit, ty);:}
    ;
procedure               ::= PROC IDENT:ident LPAREN parameterList:params RPAREN LCURL vardecs:decls statements:stmts RCURL
                            {:RESULT = new ProcDec(identleft, identright, Sym.newSym(ident.toString()), params, decls, stmts);:}
    ;
procedure_call          ::= IDENT:ident LPAREN argumentList:expList RPAREN SEMIC {:RESULT = new CallStm(identleft, identright, Sym.newSym(ident.toString()), expList);:}
    ;
parameterList 			::= {:RESULT = new DecList();:} | parameter:dec paramListTail:decList {:RESULT = new DecList(dec, decList);:}
	;
parameter 				::= IDENT:ident COLON type:typ {:RESULT = new ParDec(identleft, identright, Sym.newSym(ident.toString()), typ, false);:} |
                            REF IDENT:ident COLON type:typ {:RESULT = new ParDec(identleft, identright, Sym.newSym(ident.toString()), typ, true);:}
	;
paramListTail 			::= {:RESULT = new DecList();:}| COMMA parameter:dec paramListTail:decList
                            {:RESULT = new DecList(dec, decList);:}
	;
argumentList			::= {:RESULT = new ExpList();:}| argument:head argumentListTail:tail
                            {:RESULT = new ExpList(head, tail);:}
	;
argumentListTail 		::= {:RESULT = new ExpList();:} | COMMA argument:head argumentListTail:tail
                            {:RESULT = new ExpList(head, tail);:}
	;
argument 				::= expression:expr
                            {:RESULT = expr;:}
	;

vardec                  ::= VAR IDENT:ident COLON type:ty SEMIC {:RESULT = new VarDec(identleft, identright, Sym.newSym(ident.toString()), ty);:}
    ;
vardecs					::= {:RESULT = new DecList();:} | vardec:vard vardecs:vards {:RESULT = new DecList(vard, vards);:}
	;
varassign               ::= refExpression:var ASGN expression:exp SEMIC {:RESULT = new AssignStm(varleft, varright, var, exp);:}
    ;
while_statement         ::= WHILE:whi LPAREN expression:exp RPAREN statement:stm {:RESULT = new WhileStm(whileft, whiright, exp, stm);:}
    ;   
if_statement            ::= IF:i LPAREN expression:exp RPAREN statement:stm
                            {:RESULT = new IfStm(ileft, iright, exp, stm, new EmptyStm(ileft, iright));:} |
                            IF:i LPAREN expression:exp RPAREN statement:stm ELSE statement:stm2
                            {:RESULT = new IfStm(ileft, iright, exp, stm, stm2);:}
    ;
statement               ::= varassign:stm {: RESULT = stm;:} | 
                            procedure_call:stm {: RESULT = stm;:}  | 
                            if_statement:stm {: RESULT = stm;:} | 
                            while_statement:stm {: RESULT = stm;:} | 
                            SEMIC:sem {:RESULT = new EmptyStm(semleft, semright);:} |
                            LCURL statements:stms RCURL {:RESULT = new CompStm(stmsleft, stmsright, stms);:}
    ;
statements				::= statement:head statements:tail {:RESULT = new StmList(head, tail);:} | {: RESULT = new StmList();:}
	;

refExpression 			::= IDENT:ident {:RESULT = new SimpleVar(identleft, identright, Sym.newSym(ident.toString()));:} |
                            refExpression:ref LBRACK expression:exp RBRACK {:RESULT = new ArrayVar(refleft, refright, ref, exp);:}
	;
expression              ::= simple_expression:exp1 EQ simple_expression:exp2 
                            {:RESULT = new OpExp(exp1left, exp1right, OpExp.EQU, exp1, exp2);:} |
                            simple_expression:exp1 NE simple_expression:exp2
                            {:RESULT = new OpExp(exp1left, exp1right, OpExp.NEQ, exp1, exp2);:} |
							simple_expression:exp1 LT simple_expression:exp2
                            {:RESULT = new OpExp(exp1left, exp1right, OpExp.LST, exp1, exp2);:} |
							simple_expression:exp1 LE simple_expression:exp2
                            {:RESULT = new OpExp(exp1left, exp1right, OpExp.LSE, exp1, exp2);:} |
							simple_expression:exp1 GT simple_expression:exp2
                            {:RESULT = new OpExp(exp1left, exp1right, OpExp.GRT, exp1, exp2);:} |
							simple_expression:exp1 GE simple_expression:exp2
                            {:RESULT = new OpExp(exp1left, exp1right, OpExp.GRE, exp1, exp2);:} |
							simple_expression:simExp {: RESULT= simExp;:}
    ; 
simple_expression       ::= term:texp {: RESULT= texp;:}|
                            term:exp1 PLUS simple_expression:exp2
                            {:RESULT = new OpExp(exp1left, exp1right, OpExp.ADD, exp1, exp2);:} |
                            term:exp1 MINUS simple_expression:exp2
                            {:RESULT = new OpExp(exp1left, exp1right, OpExp.SUB, exp1, exp2);:}
    ;
term                    ::= unary:uexp {: RESULT=uexp;:}|
                            unary:exp1 STAR term:exp2
                            {:RESULT = new OpExp(exp1left, exp1right, OpExp.MUL, exp1, exp2);:} |
                            unary:exp1 SLASH term:exp2
                            {:RESULT = new OpExp(exp1left, exp1right, OpExp.DIV, exp1, exp2);:}
    ;
unary 					::= factor:fexp {: RESULT = fexp;:}|
                            MINUS factor:exp2
                            {:RESULT = new OpExp(exp2left, exp2right, OpExp.SUB, new IntExp(exp2left, exp2right, 0), exp2);:}
	;
factor                  ::= refExpression:refExp {: RESULT= new VarExp(refExpleft, refExpright, refExp);:} | 
                            INTLIT:intlit {: RESULT = new IntExp(intlitleft, intlitright, intlit );:}| 
                            LPAREN expression:exp RPAREN {: RESULT = exp;:}
    ;